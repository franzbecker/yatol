plugins {
    id 'net.franz-becker.gradle-lombok' version '1.1'
    id 'java'
    id 'war'
    id 'eclipse-wtp'
}

sourceSets {
    integrationTest {
        java.srcDir file('src/integration-test/java')
        resources.srcDir file('src/integration-test/resources')
    }
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.12'
    compile 'org.postgresql:postgresql:9.4-1201-jdbc41'
    providedCompile 'org.jboss.spec:jboss-javaee-all-7.0:1.0.3.Final'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:1.10.19'
    testCompile 'com.google.truth:truth:0.27'
    testCompile 'com.sun.jersey:jersey-bundle:1.19'

    // Configure integrationTest
	integrationTestCompile sourceSets.main.output
	integrationTestCompile sourceSets.test.output
	integrationTestCompile configurations.testCompile
	integrationTestRuntime configurations.testRuntime
}

task integrationTest(type: Test, dependsOn: testClasses) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}

task integrationTestDocker(type: Test) {
    dependsOn testClasses, ':infra:app-server:startAppContainer',
            ':infra:app-server:getAppContainerIpAddress'

    doFirst {
        String appContainerIpAddress = System.getProperty("appContainer.ipAddress")
        String backendUrl = "http://${appContainerIpAddress}:8080/backend/"
        logger.lifecycle("Setting BACKEND_URL to ${backendUrl}")
        jvmArgs = jvmArgs + "-DBACKEND_URL=${backendUrl}"
        println "Doing some primitive waiting for Docker + JBoss"
        sleep(30000)
        println "Done waiting. Executing integration tests."
    }
    doLast {
      println "Finished integration tests."
    }
    outputs.upToDateWhen { false }
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    finalizedBy ':infra:app-server:removeAppContainer'
}
