import com.bmuschko.gradle.docker.tasks.container.*
import com.bmuschko.gradle.docker.tasks.image.*

task buildDbDockerImage(type: DockerBuildImage) {
    dockerFile = project.file('Dockerfile')
    inputDir = dockerFile.parentFile
    tag = 'yatol/db-srv'
}

task createDBContainer(type: DockerCreateContainer) {
    dependsOn buildDbDockerImage
    targetImageId { buildDbDockerImage.getImageId() }
    portBindings = ['5432:5432']
    containerName = "dbContainer"
}

task startDBContainer(type: DockerStartContainer) {
    dependsOn createDBContainer
    targetContainerId { createDBContainer.getContainerId() }
}

task stopDBContainer(type: DockerStopContainer) {
    targetContainerId { createDBContainer.getContainerId() }
}

task removeDBContainer(type: DockerRemoveContainer) {
    dependsOn stopDBContainer
    targetContainerId { createDBContainer.getContainerId() }
}

task functionalTestDBContainer {
    dependsOn startDBContainer
    finalizedBy removeDBContainer
}

task tagDockerImage(type: DockerTagImage, dependsOn: buildDbDockerImage) {
    targetImageId { buildDbDockerImage.getImageId() }
    repository = 'yatol-docker-yatol.bintray.io/yatol-db'
    tag = version
    force = true
}

task pushDockerImage(type: DockerPushImage, dependsOn: tagDockerImage) {
    imageName = 'yatol-docker-yatol.bintray.io/yatol-db'
    tag = version
}
