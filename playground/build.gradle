plugins {
  id "com.bmuschko.docker-remote-api" version "2.6.1"
}

import com.bmuschko.gradle.docker.tasks.container.*
import com.bmuschko.gradle.docker.tasks.image.*

apply plugin: 'application'

repositories.jcenter()

mainClassName = 'HelloWorld'

dependencies {
    compile 'org.eclipse.jetty.aggregate:jetty-all:9.3.6+'
    testCompile 'junit:junit:4.12'
}

clean.doFirst {
    delete project.file('docker/dist')
}

task copyDist(type: Copy) {
    dependsOn 'installDist'
    from project.file('build/install/playground/')
    into project.file('docker/dist')
}

task buildDockerImage(type: DockerBuildImage) {
    dependsOn 'copyDist'
    dockerFile = project.file('docker/Dockerfile')
    inputDir = dockerFile.parentFile
    tag = 'demo'
}

task createContainer(type: DockerCreateContainer) {
    dependsOn buildDockerImage
    targetImageId { buildDockerImage.getImageId() }
    portBindings = ['8080:8080']
    containerName = 'mydemo'
}

task startContainer(type: DockerStartContainer) {
    dependsOn createContainer
    targetContainerId { 'mydemo' }
}

task stopContainer(type: DockerStopContainer) {
    targetContainerId { 'mydemo' }
}

task removeContainer(type: DockerRemoveContainer) {
    dependsOn stopContainer
    targetContainerId { 'mydemo' }
}
